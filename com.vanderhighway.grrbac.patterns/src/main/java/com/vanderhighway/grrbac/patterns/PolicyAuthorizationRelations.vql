//*******************************************************************************
// * Policy Queries
// *******************************************************************************

package com.vanderhighway.grrbac.patterns

import "https://vanderhighway.com/grrbac/2020"
import "http://www.eclipse.org/emf/2002/Ecore"
import java ^com.vanderhighway.grrbac.aggregators.Scenario

// ----- Utility Patterns -----

pattern USD(in user: User, scenario: java Scenario, demarcation:Demarcation) {
	User.UR(user, role);
    find RHSDH(role, scenario, demarcation);
}

pattern USP(in user: User, scenario: java Scenario, permission:Permission) {
	User.UR(user,role);
    find RSP(role, scenario, permission);
}

pattern USO(in user: User, scenario: java Scenario, object:SACSObject) {
    find USP(user, scenario, permission);
    Permission.PO(permission, object);
}

pattern RSP(in role: Role, scenario: java Scenario, permission:Permission) {
	find RHSDH(role, scenario, demarcation);
    Demarcation.DP(demarcation,permission);
}

pattern RSD(in role: Role, scenario: java Scenario, demarcation: Demarcation) {
	find GrantPriority(scenario, role, demarcation, _);
	find RevokePriority(scenario, role, demarcation, _);
	maxGrantPriority == max find GrantPriority(scenario, role, demarcation, #p);
	maxRevokePriority == max find RevokePriority(scenario, role, demarcation, #p2);
	check(maxGrantPriority > maxRevokePriority);
} or {
	find GrantPriority(scenario, role, demarcation, _);
	neg find RevokePriority(scenario, role, demarcation, _);
}

pattern RHSDH(in role: Role, scenario: java Scenario, demarcation: Demarcation) {
	Role.juniors*(role, roleP);
	find RSD(roleP, scenario, demarcationP);
	Demarcation.subdemarcations*(demarcationP, demarcation);
}

pattern connectedByTemporalGrantRule(in role: Role, demarcation: Demarcation) {
	TemporalGrantRule.role(rule, role);
	TemporalGrantRule.demarcation(rule, demarcation);
}

pattern RulePriority(in scenario: java Scenario, in role: Role, demarcation: Demarcation, out priority: java Integer, isGrant: java Boolean) {
	find ScenarioTemporalContext(scenario,context);
	find foo(context, role, demarcation, priority, isGrant);
}

pattern foo(in context: TemporalContext, in role: Role, demarcation: Demarcation, out priority: java Integer, isGrant: java Boolean) {
	TemporalGrantRule(rule);
	TemporalGrantRule.temporalContext(rule, context);
	TemporalGrantRule.isGrant(rule, isGrant);
	TemporalGrantRule.role(rule, role);
	TemporalGrantRule.demarcation(rule, demarcation);
	TemporalGrantRule.priority(rule, priority);
}

pattern GrantPriority(in scenario: java Scenario, in role: Role, demarcation: Demarcation, out priority: java Integer) {
	find RulePriority(scenario, role, demarcation, priority, true);
}

pattern RevokePriority(in scenario: java Scenario, in role: Role, demarcation: Demarcation, out priority: java Integer) {
	find RulePriority(scenario, role, demarcation, priority, false);
}
